# If we don't use columnhead, the first line of the data file
# will confuse gnuplot, which will leave gaps in the plot.
set key top left outside horizontal autotitle columnhead

set xtics 1
set ytics out nomirror

set style fill solid border -1
# Make the histogram boxes half the width of their slots.
set boxwidth 0.5 relative

# Select histogram mode.
set style data histograms
# Select a row-stacked histogram.
set style histogram rowstacked

plot "server1.data" using 2:xticlabels(1) lc rgb 'green' title "Time sending Trajectories", \
	"" using 3 lc rgb 'yellow' title "Time sending Bounds",  \
	"" using 4 lc rgb 'red' title "Time receiving some messages", \
	"" using 5 lc rgb 'blue' title "Time Computing", \
	"" using 6 lc rgb 'purple' title "Time Waiting"

set term postscript color solid
set xlabel "Number of intervals"
set ylabel "Time (ms)"
set output '| ps2pdf - server.pdf'
replot