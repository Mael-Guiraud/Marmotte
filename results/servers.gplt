# If we don't use columnhead, the first line of the data file
# will confuse gnuplot, which will leave gaps in the plot.
set key top left outside horizontal autotitle columnhead

set xtics 1
set ytics out nomirror

set style fill solid border -1
# Make the histogram boxes half the width of their slots.
set boxwidth 0.5 relative

# Select histogram mode.
set style data histograms
# Select a row-stacked histogram.
set style histogram rowstacked

plot "server1-0.data" using 2:xticlabels(1) lc rgb 'green' title "traj", \
	"" using 3 lc rgb 'yellow' title "bounds",  \
	"" using 4 lc rgb 'red' title "recv", \
	"" using 5 lc rgb 'blue' title "computing"
set term postscript color solid
	
set output '| ps2pdf - server.pdf'
replot